#!/usr/bin/perl

use utf8;
use strict;
use warnings;
use open qw(:std :utf8);

use Getopt::Long;
use Pass::OTP qw(otp);
use Pass::OTP::URI qw(parse);

our $VERSION = '1.1';

my %options;
$options{secret} = pop if $ARGV[-1] !~ /^-/;

Getopt::Long::Configure(qw(auto_version auto_help));
GetOptions(\%options,
    'algorithm=s',
    'hotp',
    'totp:s',
    'base32|b',
    'counter|c=i',
    'period|time-step-size|s=s',
    'start-time|S=s',
    'now|N=s',
    'digits|d=i',
) or die("Error in command line arguments\n");

$options{type} = 'totp' if defined $options{totp};
$options{algorithm} = $options{totp} if defined $options{totp} and $options{totp} ne '';
%options = parse($options{secret}) if $options{secret} =~ m#^otpauth://#;

my $code = otp(%options);
print "$code\n";
